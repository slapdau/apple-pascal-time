{$U-}

program Kernel;

{$I GLOBALS.TEXT}

    segment procedure StartTime(dummy1, dummy2: integer);
    
    type
        byte = 0..255;
    
        clockinfo = record
            date: packed record
                month: 0..12;
                day  : 0..31;
                year : 0..100
            end {daterec};
            time: packed record
                hour   : 0..23;
                minute : 0..59;
                filler1: 0..31;
                second : 0..59;
                filler2: 0..2047;
            end {timerec}
        end {clockinfo};

    var
        hasClock:boolean;
        now:clockinfo;
        dirinfo:directory;
        bootunit:unitnum;
    
        function InitClock:boolean;
        external;
        
        procedure ReadClock(var now:clockinfo);
        external;
        
        function peek(address:integer):byte;
        
        type
            varrec = packed array[0..0] of byte;
        
        var
            mem : record case boolean of
                false : ( addr : integer );
                true : ( val : ^varrec )
            end {mem};
        
        begin
            mem.addr := address;
            peek := mem.val^[0];
        end;
        
        procedure PadTimeField(value:integer; var result:string);
        
        begin
            result := '??';
            if (value >= 0) and (value <= 99) then begin
                result[1] := chr(ord('0')+value div 10);
                result[2] := chr(ord('0')+value mod 10)
            end;
        end;

        procedure greeting;
        
        var
            version, systype: byte;
            count:integer;
            field:string;
        
        begin
            version := peek(-16607);
            systype := (peek(-16606) div 32) mod 4;
            page(output);
            for count := 1 to 8 do
                writeln;
            write('Welcome ',syvid,', to Apple II Pascal ');
            case version of
                2:writeln('1.1');
                3:writeln('1.2');
                4:writeln('1.3');
                otherwise writeln('?.?');
            end;
            writeln;
            writeln('Based on UCSD Pascal II.1');
            writeln;
            write('Current date is ');
            with thedate do begin
                write(day,'-');
                case month of
                    1 : write('Jan');
                    2 : write('Feb');
                    3 : write('Mar');
                    4 : write('Apr');
                    5 : write('May');
                    6 : write('Jun');
                    7 : write('Jul');
                    8 : write('Aug');
                    9 : write('Sep');
                    10: write('Oct');
                    11: write('Nov');
                    12: write('Dec');
                    otherwise write('???')
                end;
                writeln('-',year);
            end;
            if hasClock then
                with now.time do begin
                    write('Current time is ');
                    PadTimeField(hour,field);
                    write(field,':');
                    PadTimeField(minute,field);
                    write(field,':');
                    PadTimeField(second,field);
                    writeln(field);
                end
            else
                writeln('<No clock present>');
            writeln;
            write('Pascal system size is ');
            case systype of
                0:writeln('64K');
                1:writeln('48K');
                2:writeln('128K');
                otherwise writeln('??K');
            end;
            writeln;
            writeln;
            writeln;
            write('Copyright Apple Computer 1979,1980');
            if version >= 3 then
                write(',1983');
            if version >= 4 then
                write(',1984,1985');
            writeln;
            writeln('Copyright U.C. Regents 1979');
         end;
         
         procedure UpdtBoot;
         
         begin
             bootunit := syscom^.sysunit;
             unitread(bootunit, dirinfo, sizeof(directory), DIRBLK);
             
             if ioresult <> 0 then
                 exit(UpdtBoot);
             
             if dirinfo[0].dvid <> syvid then
                 exit(UpdtBoot);
             
             dirinfo[0].dlastboot := now.date;
             unitwrite(bootunit, dirinfo, sizeof(directory), DIRBLK);
         end;
     
     begin
         hasClock := InitClock;
         if hasClock then begin
             ReadClock(now);
             thedate := now.date;
             UpdtBoot;
         end;
         
         greetings;
         
     end;
    
begin
end.
